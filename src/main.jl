#!/usr/bin/env julia

# This plugin doesn't error by default but instead annotates the build with the error
# message and exits
FAIL_ON_ERROR = get(ENV, "BUILDKITE_PLUGIN_JULIA_COVERAGE_FAIL_ON_ERROR", "false") == "true"
function annotate_and_exit(msg)
    msg = "Coverage: $msg"
    if FAIL_ON_ERROR
        error(msg)
    else
        run(`buildkite-agent annotate $msg --style "error" --context "ctx-coverage"`)
        exit(0)
    end
end

# Early sanity checks
if !haskey(ENV, "CODECOV_TOKEN")
    annotate_and_exit("can not submit coverage without CODECOV_TOKEN")
end
const bk_repo = get(ENV, "BUILDKITE_REPO") do
    annotate_and_exit("BUILDKITE_REPO not set")
end
const bk_commit = get(ENV, "BUILDKITE_COMMIT") do
    annotate_and_exit("BUILDKITE_COMMIT not set")
end
const bk_branch = get(ENV, "BUILDKITE_BRANCH") do
    annotate_and_exit("BUILDKITE_BRANCH not set")
end
const bk_build_url = get(ENV, "BUILDKITE_BUILD_URL") do
    annotate_and_exit("BUILDKITE_BUILD_URL not set")
end
const bk_build_number = get(ENV, "BUILDKITE_BUILD_NUMBER") do
    annotate_and_exit("BUILDKITE_BUILD_NUMBER not set")
end
const bk_checkout_path = get(ENV, "BUILDKITE_BUILD_CHECKOUT_PATH") do
    annotate_and_exit("BUILDKITE_BUILD_CHECKOUT_PATH not set")
end


using Downloads: Downloads
using Pkg: Pkg, PackageSpec
using SHA: SHA

# Install CoverageTools.jl
try
    Pkg.UPDATED_REGISTRY_THIS_SESSION[] = true
    Pkg.activate("julia-coverage", shared = true)
    Pkg.add(PackageSpec(name = "CoverageTools", uuid = "c36e975a-824b-4404-a568-ef97ca766997"))
    using CoverageTools: CoverageTools
catch
    annotate_and_exit("failed to install CoverageTools.jl")
end

# Codecov CLI v0.7.5 (https://github.com/codecov/codecov-cli/releases/tag/v0.7.5
# Generated by utils/checksums.jl
const sources = Dict{Tuple{String, Union{String, Nothing}, Symbol}, Tuple{String, String}}(
    ("linux", "glibc", :x86_64) =>
        ("https://github.com/codecov/codecov-cli/releases/download/v0.7.5/codecovcli_linux", "6e36d96b22f37661f378ec7f40c7d89701d4f252d729f4213e7da4c6c2703e85"),
    ("linux", "glibc", :aarch64) =>
        ("https://github.com/codecov/codecov-cli/releases/download/v0.7.5/codecovcli_linux_arm64", "01d4130fbc9938ce70d8b0cd034ef99c298e4eb00a2455408de996de0ed9b3f2"),
    ("linux", "musl", :x86_64) =>
        ("https://github.com/codecov/codecov-cli/releases/download/v0.7.5/codecovcli_alpine_x86_64", "b0925751929b7eecbb444349fa8dc23ecb75ecf7df4e5206f46fd9662285af56"),
    ("linux", "musl", :aarch64) =>
        ("https://github.com/codecov/codecov-cli/releases/download/v0.7.5/codecovcli_alpine_arm64", "fde4120a6daa2aeb80979542963560d3b30c7031ddfca29469375974ee4e4953"),
    ("macos", nothing, :aarch64) =>
        ("https://github.com/codecov/codecov-cli/releases/download/v0.7.5/codecovcli_macos", "4e59307ae334cd780f914a3ee78af6fca29b6e81d66dd5874be9c483b4a7fd9f"),
    ("windows", nothing, :x86_64) =>
        ("https://github.com/codecov/codecov-cli/releases/download/v0.7.5/codecovcli_windows.exe", "be3d9cd59179b8048f608e38e9ba12f4b173867f8573d445815ef74076eea6c0"),
)

# Download, verify and make it executable
function download_codecovcli()
    os = Sys.islinux() ? "linux" : Sys.isapple() ? "macos" : Sys.iswindows() ? "windows" :
        annotate_and_exit("Unsupported OS: $(Sys.MACHINE)")
    libc = Sys.islinux() ? Base.BinaryPlatforms.libc(Base.BinaryPlatforms.HostPlatform()) : nothing
    arch = Sys.ARCH
    (url, shasum) = get(sources, (os, libc, arch)) do
        annotate_and_exit("Unsupported platform: $(Sys.MACHINE)")
    end
    tmpdir = mktempdir()
    exepath = joinpath(tmpdir, basename(url))
    Downloads.download(url, exepath)
    bytes = open(SHA.sha256, exepath)
    if bytes2hex(bytes) != shasum
        annotate_and_exit("shasum mismatch for codecovcli download")
    end
    chmod(exepath, filemode(exepath) | 0o100)
    return exepath
end

# Handle the `dirs` input property
function coverage_dirs()
    dirs = String[]
    for (k, v) in ENV
        if occursin(r"^BUILDKITE_PLUGIN_JULIA_COVERAGE_DIRS_[0-9]+$", k)
            push!(dirs, v)
        end
    end
    isempty(dirs) && push!(dirs, "src", "ext")
    filter!(isdir, dirs)
    isempty(dirs) && annotate_and_exit("none of the configured directories exist")
    return dirs
end


# Process the coverage files
function process_coverage(dirs)
    try
        cd(bk_checkout_path) do
            coverage_data = mapreduce(CoverageTools.process_folder, vcat, dirs)
            CoverageTools.LCOV.writefile("lcov.info", coverage_data)
        end
    catch
        annotate_and_exit("failed to process coverage files")
    end
    return
end

# Upload the data using the Codecov CLI
function upload_coverage(exepath)
    # Note 1: Apparently you have to use the undocumented command `upload-process` instead
    # of `do-upload`. `upload-process` seems to be a meta command which runs
    # `create-commit`, `create-report` and finally `do-upload`.
    # Note 2: We rely on `--auto-load-params-from BuildKite` to automatically detect most of
    # the required arguments (commit/branch/build number etc). However, we manually
    # configure the slug because the CLI uses the buildkite slug which doesn't match the
    # GitHub slug. Unless the GitHub slug is used nothing shows up in the Codecov UI...
    m = match(r"^(:?https://|git@)github.com(?:/|:)(?<slug>.+)\.git$", bk_repo)
    if m === nothing
        annotate_and_exit("failed to extract GitHub slug from BUILDKITE_REPO ($bk_repo)")
    end
    slug = String(m[:slug])
    codecov_token = ENV["CODECOV_TOKEN"]
    cmd = ```
    $(exepath)
        --auto-load-params-from BuildKite
        upload-process
        --slug $slug
        --token $codecov_token
        --plugin noop
        --disable-search
        --file lcov.info
    ```
    if FAIL_ON_ERROR
        cmd = `$cmd --fail-on-error`
    else
        cmd = ignorestatus(cmd)
    end
    cmd = setenv(cmd; dir = bk_checkout_path)
    # This is behind an environment variable so that we can skip submission when testing the
    # plugin itself.
    if get(ENV, "JULIA_COVERAGE_SUBMIT", "true") == "true"
        run(cmd)
    end
    return
end

function main()
    dirs = coverage_dirs()
    process_coverage(dirs)
    exepath = download_codecovcli()
    upload_coverage(exepath)
end

if abspath(PROGRAM_FILE) == @__FILE__
    try
        main()
    catch e
        err = sprint(showerror, e)
        annotate_and_exit("failed to process and upload coverage: $err")
    end
end
