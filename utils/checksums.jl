#!/usr/bin/env julia

using Downloads, SHA

const cliversion = v"0.7.5"
const base_url = "https://github.com/codecov/codecov-cli/releases/download/v0.7.5/"

io = IOBuffer()

println(io, "# Codecov CLI v$(cliversion) (https://github.com/codecov/codecov-cli/releases/tag/v$(cliversion)")
println(io, "# Generated by utils/checksums.jl")
println(io, "const sources = Dict{Tuple{String, Union{String, Nothing}, Symbol}, Tuple{String, String}}(")

for (os, libc, arch, suffix) in (
        ("linux", "glibc", "x86_64", "linux"),
        ("linux", "glibc", "aarch64", "linux_arm64"),
        ("linux", "musl", "x86_64", "alpine_x86_64"),
        ("linux", "musl", "aarch64", "alpine_arm64"),
        ("macos", nothing, "aarch64", "macos"),
        ("windows", nothing, "x86_64", "windows.exe"),
    )
    url = "https://github.com/codecov/codecov-cli/releases/download/v0.7.5/codecovcli_$(suffix)"
    bs = Base.BufferStream()
    tsk = @async sha256(bs)
    Downloads.download(url, bs)
    close(bs)
    sha256sum = bytes2hex(fetch(tsk))
    println(io, "    (", repr(os), ", ", repr(libc), ", ", repr(Symbol(arch)), ") =>")
    println(io, "        (" * repr(url) * ", ", repr(sha256sum), "),")
end
println(io, ")")
write(stdout, seekstart(io))
